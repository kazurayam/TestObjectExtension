plugins {
    id 'groovy'
    id 'maven-publish'
    id 'signing'
}

group = 'com.kazurayam'
archivesBaseName = "TestObjectExtension"
version = '0.1.2'

ext {
  isReleaseVersion = ! version.endsWith("SNAPSHOT")
  groovyVersion   = '2.4.7'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each {
  it.options.encoding = defaultEncoding
}
tasks.withType(GroovyCompile).each {
  it.options.encoding = defaultEncoding
}

configurations {
    generateDocs
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
  compileOnly "org.codehaus.groovy:groovy-all:${groovyVersion}"
  compileOnly fileTree(dir: "/Applications/Katalon Studio.app/Contents/Eclipse/plugins", include: ['*.jar'])
  compileOnly fileTree(dir: "/Applications/Katalon Studio.app/Contents/Eclipse/configuration/resources/lib", include: ['*.jar'])
  generateDocs "org.codehaus.groovy:groovy-all:${groovyVersion}"
}

sourceSets {
  main {
    groovy {
      srcDirs = ['Keywords', 'Libs']
      excludes = ["CustomKeywords.groovy", "Temp*.groovy"]
    }
  }
  test {
    groovy {
      srcDirs = ['Include/scripts/groovy']
    }
  }
}

task groovydoc(type: Groovydoc, overwrite:true) {
  group = 'com.kazurayam'
  source = sourceSets.main.groovy
  classpath = configurations.compileClasspath
  groovyClasspath = project.configurations.generateDocs
  include 'com/kazurayam/**'
  exclude '**/*Test.groovy'
}
task publishGroovydoc(type: Copy) {
    from 'build/docs/groovydoc'
    into 'docs/api'
}
groovydoc.finalizedBy publishGroovydoc

task groovydocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from groovydoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        testobjectextension(MavenPublication) {
            artifact sourcesJar
            artifact groovydocJar
            pom {
                groupId = project.group
                name = project.rootProject.name
                description = 'A Katalon Studio plugin that extends the TestObject class with new methods'
                url = 'https://kazurayam.github.io/TestObjectExtention/'
                from components.java
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org.licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kazurayam'
                        name = 'URAYAMA,kazuaki'
                        email = 'kazuaki.urayama@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/kazurayam/${project.rootProject.name}.git"
                    developerConnection = "scm:git:git@github.com:kazurayam/${project.rootProject.name}.git"
                    url = "https://github.com/kazurayam/${project.rootProject.name}"
                }
            }
        }
    }
    repositories {
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo: snapshotRepo
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
        }
    }
}

signing {
    sign publishing.publications.testobjectextension
}
// I do not want to sign the SNAPSHOT
/*
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
*/